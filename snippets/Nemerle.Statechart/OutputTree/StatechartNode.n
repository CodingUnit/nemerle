using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Diagnostics;
using System.Collections.Generic;
using System.Linq;
using Nemerle.Compiler;

namespace Nemerle.Statechart
{

  [Record]  
  public class StateEvent : IEquatable[StateEvent]
  {
    public evt : TransitionEvent;
    //public InternalOnly : bool {get;set;}

    public static Completion : StateEvent = StateEvent(TransitionEvent.Completion);
    
    public Location : Location
    {
      get
      {
        evt.Location
      }
    }
    
    [Memoized]
    public TargetName : string
    {
      get
      {
        Name.ToUpperFirst()
      }
    }
    
    public Name : string
    {
      get
      {
        if (evt.name != null) evt.name else "Completion"
      }
    }

    public CompareTo(b : StateEvent) : int
    {
      Name.CompareTo(b.Name)
    }

    public static @== (a : StateEvent, b : StateEvent) : bool
    {
      a.Name == b.Name
    }

    public IsCompletion : bool
    {
      get
      {
        evt.name == null
      }
    }

    public override GetHashCode() : int
    {
      Name.GetHashCode()
    }
    
    public Equals(obj : StateEvent) : bool
    {
      this.Name == obj.Name
    }
    
    public override ToString() : string
    {
      $"$Name"
    }
  }


  [Record]    
  public variant StateAction
  {
    | Entry
      {
        action : FsmAction;
        
        Name : string
        {
          get
          {
            $"Entry$(state)"
          }
        }

        flags : ActionFlags;

        override ToString() : string
        {
          Name
        }

      }
    | Exit
      {
        action : FsmAction;

        Name : string
        {
          get
          {
            $"Exit$(state)"
          }
        }

        flags : ActionFlags;

        override ToString() : string
        {
          Name
        }
      }

    | Do 
      {
        [FlagAccessor(NoCancel)]
        public flags : ActivityFlag; // shows that we must run each action concurrently with others

        action : Actions;
        
        need_completion : bool {get;set;default false}
        
        Name : string
        {
          get
          {
            $"DoActivity$state"
          }
        }

        override ToString() : string
        {
          Name
        }
      }       

    public state : string {get;set;}
  }

}
