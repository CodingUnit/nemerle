using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Extensions;
using Nemerle.Utility;
using Nemerle.Text;
using Nemerle.WPF;

using System;
using System.ComponentModel;
using System.Collections.Generic;
using System.Diagnostics;
using System.Linq;
using System.Windows;
using System.Windows.Controls;
using System.Windows.Media;

using System.Console;

[NotifyPropertyChanged]
public class Person
{
    public Age       : int { get { DateTime.Today.Year - BirthDate.Year } }

    [NotifyChangedOptions(Dependent = [Age, FullNameAndAge], HideSelfChanges)]
    public BirthDate : DateTime { get; set; }
    
    [NotifyChangedOptions(Dependent = FullNameAndAge)]
    public FirstName : string { get; set; }
    
    [NotifyChangedOptions(Dependent = FullNameAndAge)]
    public LastName  : string { get; set; }
    
    public FullNameAndAge  : string { get { $"$FirstName $LastName $Age"} }
    
    [NotifyChangedOptions(HideSelfChanges)]
    public IgnoredProperty : string { get; set; }
    
    public RaiseIgnoredPropertyChanged() : void
    {
        RaisePropertyChanged(IgnoredProperty);
    }
    
    public RaiseAllPropertiesChanged() : void
    {
        RaisePropertyChanged(null); //"null for the propertyName parameter indicates that all of the properties have changed" (http://msdn.microsoft.com/en-us/library/system.componentmodel.propertychangedeventargs.propertyname.aspx)
    }
}

[NotifyPropertyChanged]
public class Student : Person
{
    public Id : uint { get; set; }
}

public module PersonTests
{
    public Run() : void
    {
        def person = Person();
        def eventData = Dictionary();
        person.PropertyChanged += (_, e) => { if (eventData.ContainsKey(e.PropertyName)) eventData[e.PropertyName] += 1 else eventData[e.PropertyName] = 1 }
        
        person.BirthDate = DateTime.Now;
        assert(eventData["Age"] == 1);
        assert(eventData["FullNameAndAge"] == 1);
        assert(!eventData.ContainsKey("BirthDate")); //BirthDate marked with HideSelfChanges option 
        
        eventData.Clear();
        person.IgnoredProperty = "value";
        assert(!eventData.Any());
        
        eventData.Clear();
        person.RaiseIgnoredPropertyChanged();
        assert(eventData["IgnoredProperty"] == 1);
        
        mutable allPropsChanged;
        def person1 = Person();
        person1.PropertyChanged += (_, e) => { allPropsChanged = (e.PropertyName == null) }
        person1.RaiseAllPropertiesChanged();
        assert(allPropsChanged);
        
        eventData.Clear();
        def student = Student();
        student.PropertyChanged += (_, e) => { if (eventData.ContainsKey(e.PropertyName)) eventData[e.PropertyName] += 1 else eventData[e.PropertyName] = 1 };
        student.Id = 123;
        assert(eventData["Id"] == 1);
    }
}

//negative tests

//[NotifyPropertyChanged]
//public delegate Foo() : void; //error : Macro NotifyPropertyChanged is not valid on this declaration type. It is only valid on 'class' declarations.

//[NotifyPropertyChanged] //error : Duplicates of 'NotifyPropertyChanged' macroattribute are not allowed.
//[NotifyPropertyChanged]
//class A {}

//[NotifyPropertyChanged("Method")]//error : Specify a valid method for property changed event raising (e.g. NotifyPropertyChanged(OnPropertyChanged))
//class A1 {}

//[NotifyPropertyChanged]
//public class A2
//{
//    [NotifyChangedOptions(HideSelfChanges)] //warning : NotifyChangedOptions macro ignored on property without a setter.
//    public Prop       : int { get; }

//    [NotifyChangedOptions(Dependent = [Prop3, Prop], HideSelfChanges)] //error : Class 'A2' does not contain a property with the name 'Prop3'.
//    public Prop2 : DateTime { get; set; }
    
//    [NotifyChangedOptions(Dependent = ["Prop3 + 7"], HideSelfChanges)] //error : Expected simple name.
//    public Prop4 : DateTime { get; set; }
    
//    public CustomNotify() : void
//    {
//        RaisePropertyChanged("test"+7); //error : Specify a property for changed event raising (e.g. RaisePropertyChanged(PropName)).
//        RaisePropertyChanged(Prop5); //error : Class 'A2' does not contain a property with the name 'Prop5'.
//        RaisePropertyChanged(Prop, Raise); //error : Specify a valid method for property changed event raising (e.g. RaisePropertyChanged(PropName, OnPropertyChanged)).
//    }
//}